# Maintainer: Wouter Wijsman <wwijsman@live.nl>
pkgname=angelscript
pkgver=2.28.2
pkgrel=1
pkgdesc="A game-oriented interpreted compiled scripting language library"
arch=('mips')
url="http://angelcode.com/angelscript/"
license=('ZLIB')
depends=()
makedepends=()
optdepends=()
source=("http://www.angelcode.com/angelscript/sdk/files/${pkgname}_${pkgver}.zip")
sha256sums=('54bc6126d2ba2509f3916ff139758d3c3c2185dddb521fc040110c5afde6000a')

build() {
    cd "sdk/${pkgname}/projects/cmake"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "sdk/${pkgname}/"
    mkdir -m 755 -p "$pkgdir/psp/lib" "$pkgdir/psp/include"
    install -m 644 "lib/libAngelscript.a" "$pkgdir/psp/lib"
    install -m 644 "include/angelscript.h" "$pkgdir/psp/include"

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 ../docs/manual/doc_license.html "$pkgdir/psp/share/licenses/$pkgname/license.html"

    # Install documentation
    mkdir -p "$pkgdir/psp/share/doc/"
    cp -r ../docs "$pkgdir/psp/share/doc/$pkgname"
}

