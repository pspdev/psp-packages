pkgname=bzip2
pkgver=1.0.8
pkgrel=3
pkgdesc="A high-quality data compression library"
arch=('mips')
url="http://www.sourceware.org/bzip2/"
license=('custom')
depends=()
makedepends=()
optdepends=()
source=("git+https://gitlab.com/bzip2/bzip2.git#commit=bf5f505d0156ad5c6635d05db06b1bb7593b45b7")
sha256sums=('SKIP')

prepare() {
    cd "$pkgname"
    sed -i 's#@prefix@#${PSPDEV}/psp#' bzip2.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' bzip2.pc.in
    sed -i 's#@bindir@#${prefix}/bin#' bzip2.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' bzip2.pc.in
    sed -i 's#@includedir@#${prefix}/include#' bzip2.pc.in
}

build() {
    cd "$pkgname"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        -DENABLE_SHARED_LIB=OFF -DENABLE_STATIC_LIB=ON -DENABLE_LIB_ONLY=ON "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "$pkgname/build"
    make --quiet $MAKEFLAGS install || { exit 1; }
    cd ..

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING "$pkgdir/psp/share/licenses/$pkgname"

    # The library gets built as libbz2_static.a, let's rename it since the default pkgconfig lib is obviously -lbz2
    mv "$pkgdir/psp/lib/libbz2_static.a" "$pkgdir/psp/lib/libbz2.a"
}

