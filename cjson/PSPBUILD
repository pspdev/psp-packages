pkgname=cjson
pkgver=1.7.16
pkgrel=4
pkgdesc="Ultralightweight JSON parser in ANSI C."
arch=(any)
url="https://github.com/DaveGamble/cJSON/"
license=('MIT')
groups=('pspdev-default')
depends=()
makedepends=()
optdepends=()
source=("https://github.com/DaveGamble/cJSON/archive/refs/tags/v1.7.16.tar.gz")
sha256sums=('451131a92c55efc5457276807fc0c4c2c2707c9ee96ef90c47d68852d5384c6c')

prepare() {
    cd "cJSON-$pkgver"
    sed -i 's#@CMAKE_INSTALL_FULL_LIBDIR@#${PSPDEV}/psp/lib#' library_config/libcjson.pc.in
    sed -i 's#@CMAKE_INSTALL_FULL_LIBDIR@#${PSPDEV}/psp/lib#' library_config/libcjson_utils.pc.in
    sed -i 's#@CMAKE_INSTALL_FULL_INCLUDEDIR@#${PSPDEV}/psp/include#' library_config/libcjson.pc.in
    sed -i 's#@CMAKE_INSTALL_FULL_INCLUDEDIR@#${PSPDEV}/psp/include#' library_config/libcjson_utils.pc.in
    mkdir -p build
}

build() {
    cd "cJSON-$pkgver/build"
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        -DENABLE_CJSON_UTILS=On -DENABLE_LOCALES=OFF -DENABLE_CJSON_TEST=OFF -DENABLE_TARGET_EXPORT=OFF -DCMAKE_BUILD_TYPE=Release \
        "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "cJSON-$pkgver/build"
    make --quiet $MAKEFLAGS install
    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 ../LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}
