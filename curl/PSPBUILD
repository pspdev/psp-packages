pkgname=curl
pkgver=7.64.1
pkgrel=9
pkgdesc="A library for transferring data with URL syntax"
arch=('mips')
url="https://curl.se/"
license=('custom')
groups=('pspdev-default')
depends=('zlib' 'mbedtls')
optdepends=()
makedepends=()
source=(
    "https://curl.haxx.se/download/${pkgname}-${pkgver}.tar.gz"
    "${pkgname}-${pkgver}-PSP.patch"
)
sha256sums=(
    "432d3f466644b9416bc5b649d344116a753aeaa520c8beaf024a90cba9d3d35d"
    "SKIP"
)

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -p1 < ../${pkgname}-${pkgver}-PSP.patch || { exit 1; }
    sed -i 's#@prefix@#${PSPDEV}/psp#' libcurl.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' libcurl.pc.in
}

build() {
    cd "$pkgname-$pkgver"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DCMAKE_USE_MBEDTLS=1 -DUSE_UNIX_SOCKETS=0 \
        -DENABLE_IPV6=0 -DENABLE_THREADED_RESOLVER=0 -DBUILD_CURL_EXE=OFF -DBUILD_SHARED_LIBS=OFF -DBUILD_TESTING=OFF -DCMAKE_BUILD_TYPE=Release \
        "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "$pkgname-$pkgver/build"
    make --quiet $MAKEFLAGS install
    cd ..
}
