pkgname=enet
pkgver=1.3.18
pkgrel=3
pkgdesc="ENet reliable UDP networking library"
arch=('mips')
url="http://enet.bespin.org/"
license=('MIT')
groups=('pspdev-default')
depends=()
makedepends=()
optdepends=()
source=("https://github.com/lsalzman/enet/archive/v${pkgver}.tar.gz")
sha256sums=('28603c895f9ed24a846478180ee72c7376b39b4bb1287b73877e5eae7d96b0dd')

prepare() {
    cd "$pkgname-$pkgver"
    # if ARCHIVE DESTINATION is not specified, static library will be installed to LIBRARY DESTINATION
    sed -i '/ARCHIVE DESTINATION/d' CMakeLists.txt
}

build() {
    cd "$pkgname-$pkgver"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        -DCMAKE_BUILD_TYPE=Release "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "$pkgname-$pkgver"
    make install -C build

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}

