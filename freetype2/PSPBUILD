pkgname=freetype2
pkgver=2.11.0
pkgrel=8
pkgdesc="A freely available software library to render fonts"
arch=(any)
url="https://www.freetype.org/"
license=('FTL OR GPL-2.0-or-later')
groups=('pspdev-default')
depends=('zlib' 'bzip2' 'libpng')
makedepends=()
optdepends=()
group=('all')
source=("https://github.com/freetype/freetype/archive/VER-${pkgver//./-}.tar.gz")
sha256sums=('fffcba25febd1581cc12d706a39bee96f37eab627d0838ce6e8189ebd841d722')

prepare() {
    cd "freetype-VER-${pkgver//./-}/builds/unix"
    sed -i 's#%prefix%#${PSPDEV}/psp#' freetype2.in
    sed -i 's#%exec_prefix%#${prefix}#' freetype2.in
    sed -i 's#%libdir%#${prefix}/lib#' freetype2.in
    sed -i 's#%includedir%#${prefix}/include#' freetype2.in
}

build() {
    cd "freetype-VER-${pkgver//./-}"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=/psp -DBUILD_SHARED_LIBS=OFF \
        -DCMAKE_BUILD_TYPE=Release -DCMAKE_POLICY_VERSION_MINIMUM=3.5 "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "freetype-VER-${pkgver//./-}/build"
    make --quiet DESTDIR=$pkgdir $MAKEFLAGS install
    cd ..

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE.TXT "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 docs/GPLv2.TXT "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 docs/FTL.TXT "$pkgdir/psp/share/licenses/$pkgname"
}

