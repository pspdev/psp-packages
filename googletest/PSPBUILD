pkgname=googletest
pkgver=1.14.0
pkgrel=1
pkgdesc="GoogleTest - Google Testing and Mocking Framework"
arch=('mips')
url="google.github.io/googletest/"
license=('BSD')
depends=()
makedepends=()
optdepends=()
source=("https://github.com/google/googletest/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('8ad598c73ad796e0d8280b082cebd82a630d73e73cd3c70057938a6501bba5d7')

prepare() {
    cd "$pkgname-$pkgver"
    sed -i 's#@CMAKE_INSTALL_FULL_LIBDIR@#${prefix}/lib#' googlemock/cmake/gmock_main.pc.in
    sed -i 's#@CMAKE_INSTALL_FULL_INCLUDEDIR@#${prefix}/include#' googlemock/cmake/gmock_main.pc.in
    sed -i 's#@CMAKE_INSTALL_FULL_LIBDIR@#${prefix}/lib#' googlemock/cmake/gmock.pc.in
    sed -i 's#@CMAKE_INSTALL_FULL_INCLUDEDIR@#${prefix}/include#' googlemock/cmake/gmock.pc.in
    sed -i 's#@CMAKE_INSTALL_FULL_LIBDIR@#${prefix}/lib#' googletest/cmake/gtest_main.pc.in
    sed -i 's#@CMAKE_INSTALL_FULL_INCLUDEDIR@#${prefix}/include#' googletest/cmake/gtest_main.pc.in
    sed -i 's#@CMAKE_INSTALL_FULL_LIBDIR@#${prefix}/lib#' googletest/cmake/gtest.pc.in
    sed -i 's#@CMAKE_INSTALL_FULL_INCLUDEDIR@#${prefix}/include#' googletest/cmake/gtest.pc.in
}

build() {
    cd "$pkgname-$pkgver"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp \
        -DCMAKE_BUILD_TYPE=Release -DBUILD_SHARED_LIBS=OFF -DCMAKE_CXX_FLAGS='-DGTEST_HAS_POSIX_RE=0' \
        "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "$pkgname-$pkgver/build"
    make --quiet $MAKEFLAGS install
    cd ..

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}
