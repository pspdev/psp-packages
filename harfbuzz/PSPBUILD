pkgname=harfbuzz
pkgver=8.5.0
pkgrel=4
pkgdesc="OpenType text shaping engine"
arch=(any)
url="https://harfbuzz.github.io/"
license=('MIT')
depends=('freetype2')
groups=('pspdev-default')
makedepends=()
optdepends=()
source=(
  "https://github.com/harfbuzz/harfbuzz/releases/download/${pkgver}/harfbuzz-${pkgver}.tar.xz"
  "fix-cmake.patch"
)
sha256sums=(
  "77e4f7f98f3d86bf8788b53e6832fb96279956e1c3961988ea3d4b7ca41ddc27"
  "SKIP"
)

prepare() {
    cd "harfbuzz-${pkgver}/src"
    sed -i 's#%prefix%#${PSPDEV}/psp#' harfbuzz*.pc.in
    sed -i 's#%exec_prefix%#${prefix}#' harfbuzz*.pc.in
    sed -i 's#%libdir%#${prefix}/lib#' harfbuzz*.pc.in
    sed -i 's#%includedir%#${prefix}/include#' harfbuzz*.pc.in

    cd ..
    patch -p1 < ../fix-cmake.patch
}

build() {
    cd "harfbuzz-${pkgver}"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        -DCMAKE_BUILD_TYPE=Release -DHB_HAVE_FREETYPE=ON "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "harfbuzz-${pkgver}/build"
    make --quiet $MAKEFLAGS install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 ../COPYING "$pkgdir/psp/share/licenses/$pkgname"
}
