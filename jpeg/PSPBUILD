pkgname=jpeg
pkgver=3.1.3
pkgrel=7
pkgdesc="A free library for JPEG image compression"
arch=(any)
url="https://ijg.org/"
license=('custom')
groups=('pspdev-default')
depends=()
makedepends=()
optdepends=()
source=("https://github.com/libjpeg-turbo/libjpeg-turbo/archive/${pkgver}.tar.gz")
sha256sums=('3a13a5ba767dc8264bc40b185e41368a80d5d5f945944d1dbaa4b2fb0099f4e5')

prepare() {
    cd "libjpeg-turbo-${pkgver}/release"
    sed -i 's#@CMAKE_INSTALL_PREFIX@#${PSPDEV}/psp#g' libjpeg.pc.in libturbojpeg.pc.in
    sed -i 's#@CMAKE_INSTALL_FULL_LIBDIR@#${prefix}/lib#' libjpeg.pc.in libturbojpeg.pc.in
    sed -i 's#@CMAKE_INSTALL_FULL_INCLUDEDIR@#${prefix}/include#' libjpeg.pc.in libturbojpeg.pc.in
}

build() {
    cd "libjpeg-turbo-${pkgver}"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        -DCMAKE_BUILD_TYPE=Release -DENABLE_SHARED=FALSE -DWITH_SIMD=0 "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "libjpeg-turbo-${pkgver}/build"
    make --quiet $MAKEFLAGS install
    cd ..

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 README.ijg "$pkgdir/psp/share/licenses/$pkgname"

    # Build system produces a bunch of ELF binaries we never use, can't disable it, just delete them
    rm -f "$pkgdir/psp/bin/tjbench"
    rm -f "$pkgdir/psp/bin/cjpeg"
    rm -f "$pkgdir/psp/bin/djpeg"
    rm -f "$pkgdir/psp/bin/jpegtran"
    rm -f "$pkgdir/psp/bin/rdjpgcom"
    rm -f "$pkgdir/psp/bin/wrjpgcom"
}

