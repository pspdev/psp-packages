pkgname=libeigen
pkgver=3.4.0
pkgrel=4
url="http://eigen.tuxfamily.org"
source=("https://gitlab.com/libeigen/eigen/-/archive/3.4.0/eigen-$pkgver.tar.gz")
license=('MPL-2.0 AND BSD-3-Clause AND LGPL-2.1-only AND LGPL-2.1-or-later')
groups=('pspdev-default')
depends=()
makedepends=()
pkgdesc="C++ template library for linear algebra: matrices, vectors, numerical solvers, and related algorithms"
arch=(any)
sha256sums=('8586084f71f9bde545ee7fa6d00288b264a2b7ac3607b974e54d13e7162c1c72')

prepare() {
    cd "$srcdir/eigen-$pkgver"
    sed -i 's#@CMAKE_INSTALL_PREFIX@#${PSPDEV}/psp#' eigen3.pc.in
}

build() {
  cd eigen-$pkgver
  mkdir -p build
  cd build
  cmake .. -DCMAKE_TOOLCHAIN_FILE="$PSPDEV/psp/share/pspdev.cmake" -DCMAKE_INSTALL_PREFIX="/psp" -DCMAKE_BUILD_TYPE=Release -Wno-dev -DBUILD_SHARED_LIBS=OFF -DBUILD_TESTING=OFF
  make $MAKEFLAGS || { exit 1; }
}

package() {
    ( 
        cd eigen-$pkgver/build
        make DESTDIR="$pkgdir" install
    )

	mkdir -p "$pkgdir/psp/lib/cmake"
	mv "$pkgdir/psp/share/eigen3/cmake" "$pkgdir/psp/lib/cmake/eigen3"
	rmdir "$pkgdir/psp/share/eigen3"

    install -Dm644 "$srcdir/eigen-$pkgver/COPYING.BSD" "$pkgdir/psp/share/licenses/$pkgname/COPYING.BSD"
}
