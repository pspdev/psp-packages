pkgname=liblzma
pkgver=5.2.10
pkgrel=1
pkgdesc="LZMA provides a general-purpose data-compression library"
arch=('mips')
url="https://github.com/tukaani-project/xz"
license=('BSD-0-Clause')
depends=()
groups=('pspdev-default')
makedepends=()
optdepends=()
source=(
    "https://github.com/tukaani-project/xz/releases/download/v5.2.10/xz-${pkgver}.tar.bz2"
)
sha256sums=(
    "01b71df61521d9da698ce3c33148bff06a131628ff037398c09482f3a26e5408"
)

prepare() {
    cd "xz-${pkgver}/src/${pkgname}"

    sed -i 's#@prefix@#${PSPDEV}/psp#' liblzma.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' liblzma.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' liblzma.pc.in
    sed -i 's#@includedir@#${prefix}/include#' liblzma.pc.in
}

build() {
    cd "xz-${pkgver}"

    ./autogen.sh --no-po4a --host=psp
    ./configure --host=psp --disable-shared --enable-static --prefix=/psp

    cd "src/${pkgname}"
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "xz-${pkgver}/src/${pkgname}"
    make --quiet DESTDIR="$pkgdir" $MAKEFLAGS install

    rm "${pkgdir}/psp/lib/liblzma.la"

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 ../../COPYING "$pkgdir/psp/share/licenses/$pkgname"
}
