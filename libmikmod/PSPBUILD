pkgname=libmikmod
pkgver=3.3.11.1
pkgrel=9
pkgdesc="A portable sound library capable of playing samples as well as module files"
arch=(any)
url="http://mikmod.sourceforge.net/"
license=('LGPL-2.1-only')
groups=('pspdev-default')
depends=()
makedepends=()
optdepends=()
source=("git+https://git.code.sf.net/p/mikmod/mikmod#commit=65efd9e7b6cec78e91af1a1e664eadffab934888")
sha256sums=('SKIP')

prepare() {
    cd mikmod/$pkgname
    sed -i 's#@prefix@#${PSPDEV}/psp#' libmikmod.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' libmikmod.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' libmikmod.pc.in
    sed -i 's#@includedir@#${prefix}/include#' libmikmod.pc.in
}

build() {
    cd mikmod/$pkgname
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DENABLE_SHARED=0 \
        -DCMAKE_BUILD_TYPE=Release -DCMAKE_POLICY_VERSION_MINIMUM=3.5 "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package () {
    cd "mikmod/$pkgname/build"
    make --quiet $MAKEFLAGS install
    cd ..

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname/"
    install -m 644 COPYING.LESSER "$pkgdir/psp/share/licenses/$pkgname/"
    install -m 644 AUTHORS "$pkgdir/psp/share/licenses/$pkgname/"
}
