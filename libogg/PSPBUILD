pkgname=libogg
pkgver=1.3.5
pkgrel=4
pkgdesc="library for the multimedia container format, and the native file and stream format ogg"
arch=('mips')
url="https://www.xiph.org/ogg/"
license=('BSD')
groups=('pspdev-default')
depends=()
makedepends=()
optdepends=()
source=("https://github.com/xiph/ogg/archive/v${pkgver}.tar.gz")
sha256sums=('f6f1b04cfa4e98b70ffe775d5e302d9c6b98541f05159af6de2d6617817ed7d6')

prepare() {
    cd "ogg-${pkgver}"
    sed -i 's#@prefix@#${PSPDEV}/psp#' ogg.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' ogg.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' ogg.pc.in
    sed -i 's#@includedir@#${prefix}/include#' ogg.pc.in
}

build() {   
    cd "ogg-${pkgver}"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        -DCMAKE_BUILD_TYPE=Release "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
    cd ../..
}

package() {
    cd "ogg-${pkgver}/build"
    make --quiet $MAKEFLAGS install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 ../COPYING "$pkgdir/psp/share/licenses/$pkgname"
}

