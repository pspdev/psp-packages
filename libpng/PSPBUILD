pkgname=libpng
pkgver=1.6.43
pkgrel=8
pkgdesc="libpng is the official PNG reference library"
arch=('mips')
url="http://libpng.org/pub/png/libpng.html"
license=('libpng-2.0')
groups=('pspdev-default')
depends=('zlib')
makedepends=()
optdepends=()
source=("https://github.com/pnggroup/libpng/archive/v${pkgver}.tar.gz")
sha256sums=('fecc95b46cf05e8e3fc8a414750e0ba5aad00d89e9fdf175e94ff041caf1a03a')

prepare() {
    cd "$pkgname-${pkgver}"
    sed -i 's#@prefix@#${PSPDEV}/psp#' libpng-config.in libpng.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' libpng-config.in libpng.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' libpng-config.in libpng.pc.in
    sed -i 's#@includedir@/libpng@PNGLIB_MAJOR@@PNGLIB_MINOR@#${prefix}/include/libpng@PNGLIB_MAJOR@@PNGLIB_MINOR@#' libpng-config.in libpng.pc.in
}

build() {
    cd "$pkgname-${pkgver}"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=/psp -DBUILD_SHARED_LIBS=OFF \
        -DPNG_SHARED=OFF -DPNG_STATIC=ON -DUNIX=ON -DCMAKE_BUILD_TYPE=Release "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "$pkgname-${pkgver}/build"
    make --quiet DESTDIR=$pkgdir $MAKEFLAGS install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 ../LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}
