pkgname=libpspvram
pkgver=r11.885fd3f
pkgrel=3
pkgdesc="Dynamic VRAM allocation manager for the PSP"
arch=('mips')
url="https://github.com/albe/libpspvram"
license=('MIT')
groups=('pspdev-default')
depends=()
makedepends=()
optdepends=()
source=("git+https://github.com/pspdev/libpspvram.git#commit=885fd3f5420dbc6a9ed450d268d752813093b417")
sha256sums=('SKIP')

pkgver() {
    cd "$pkgname"
    # Set the revision of the repo as version
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$pkgname"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp \
        -DCMAKE_BUILD_TYPE=Release "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "$pkgname/build"
    make --quiet $MAKEFLAGS install

    cd ..
    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}

