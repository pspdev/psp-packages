pkgname=libvorbis
pkgver=1.3.7
pkgrel=5
pkgdesc="Vorbis audio compression reference implementation"
arch=('mips')
url="https://gitlab.xiph.org/xiph/vorbis"
license=('BSD-3-Clause')
groups=('pspdev-default')
depends=('libogg')
makedepends=()
optdepends=()
source=("https://github.com/xiph/vorbis/archive/v${pkgver}.tar.gz")
sha256sums=('270c76933d0934e42c5ee0a54a36280e2d87af1de3cc3e584806357e237afd13')

prepare() {
    cd "vorbis-${pkgver}"
    sed -i 's#@prefix@#${PSPDEV}/psp#' vorbisenc.pc.in vorbisfile.pc.in vorbis.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' vorbisenc.pc.in vorbisfile.pc.in vorbis.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' vorbisenc.pc.in vorbisfile.pc.in vorbis.pc.in
    sed -i 's#@includedir@#${prefix}/include#' vorbisenc.pc.in vorbisfile.pc.in vorbis.pc.in
}

build() {
    cd "vorbis-${pkgver}"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        -DUNIX:BOOL=ON -DCMAKE_BUILD_TYPE=Release "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "vorbis-${pkgver}/build"
    make --quiet $MAKEFLAGS install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 ../COPYING "$pkgdir/psp/share/licenses/$pkgname"
}

