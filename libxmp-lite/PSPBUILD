pkgname=libxmp-lite
pkgver=4.6.3
pkgrel=5
pkgdesc="Libxmp is a library that renders module files to PCM data"
arch=(any)
url="https://github.com/libxmp/libxmp"
license=('MIT')
groups=('pspdev-default')
depends=()
makedepends=()
optdepends=()
source=("https://github.com/libxmp/libxmp/releases/download/libxmp-${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('fa6465d8b911363ae602c7baaa625ca1f5223142d10bb4682029c4d2f630cb62')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i 's#@prefix@#${PSPDEV}/psp#' libxmp-lite.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' libxmp-lite.pc.in
    sed -i 's#@includedir@#${prefix}/include#' libxmp-lite.pc.in
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir -p build && cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE="${PSPDEV}/psp/share/pspdev.cmake" -DCMAKE_INSTALL_PREFIX=/psp -DBUILD_SHARED=OFF \
        -DCMAKE_BUILD_TYPE=Release -DCMAKE_POLICY_VERSION_MINIMUM=3.5 "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    make --quiet $MAKEFLAGS DESTDIR="${pkgdir}/" install || { exit 1; }

    mkdir -m 755 -p "${pkgdir}/psp/share/licenses/${pkgname}"
    install -m 644 ../README "${pkgdir}/psp/share/licenses/${pkgname}"
}
