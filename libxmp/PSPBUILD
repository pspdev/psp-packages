pkgname=libxmp
pkgver=4.5.0
pkgrel=6
pkgdesc="Libxmp is a library that renders module files to PCM data"
arch=(any)
url="https://github.com/libxmp/libxmp"
license=('LGPL-2.1-only')
groups=('pspdev-default')
depends=()
makedepends=()
optdepends=()
source=("git+https://github.com/libxmp/libxmp.git#commit=b1766e972cebebaaf21f738ee6c26bed95c1e9ed")
sha256sums=('SKIP')

prepare() {
    cd "$pkgname"
    sed -i 's#@prefix@#${PSPDEV}/psp#' libxmp.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' libxmp.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' libxmp.pc.in
    sed -i 's#@includedir@#${prefix}/include#' libxmp.pc.in
}

build() {
    cd "$pkgname"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED=OFF \
        -DCMAKE_BUILD_TYPE=Release -DCMAKE_POLICY_VERSION_MINIMUM=3.5 "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "$pkgname/build"
    make --quiet $MAKEFLAGS install
    cd ..

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 docs/COPYING.LIB "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 docs/CREDITS "$pkgdir/psp/share/licenses/$pkgname"
}

