pkgname=lua51
pkgver=5.1.5
pkgrel=4
pkgdesc="a powerful, efficient, lightweight, embeddable scripting language (version 5.1)"
arch=('mips')
url="https://lua.org"
license=('MIT')
depends=()
makedepends=()
optdepends=()
conflicts=('lua')
provides=('lua=5.1')
source=("https://www.lua.org/ftp/lua-${pkgver}.tar.gz")
sha256sums=('2640fc56a795f29d28ef15e13c34a47e223960b0240e8cb0a82d9b0738695333')

prepare() {
    cd "lua-${pkgver}"

    # extract the license from readme
    grep -A21 Copyright doc/readme.html | \
        sed -e 's/&ndash;/–/g' \
            -e 's/&copy;/©/g' \
            -e '/<P>/d' > LICENSE
}

build() {
    cd "lua-${pkgver}/src"
    make a CC="psp-gcc" AR="psp-ar rcu" RANLIB="psp-ranlib"
}

package() {
    cd "lua-${pkgver}"
    make INSTALL_TOP="${pkgdir}/psp" INSTALL_BIN="/tmp" TO_BIN="/dev/null" \
        INSTALL_MAN="/tmp" TO_MAN="/dev/null" install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}
