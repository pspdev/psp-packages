pkgname=lua52
pkgver=5.2.4
pkgrel=2
pkgdesc="a powerful, efficient, lightweight, embeddable scripting language (version 5.2)"
arch=(any)
url="https://lua.org"
license=('MIT')
depends=()
makedepends=()
optdepends=()
conflicts=('lua')
provides=('lua=5.2')
source=("https://www.lua.org/ftp/lua-${pkgver}.tar.gz")
sha256sums=('b9e2e4aad6789b3b63a056d442f7b39f0ecfca3ae0f1fc0ae4e9614401b69f4b')

prepare() {
    cd "lua-${pkgver}"

    # extract the license from readme
    grep -A21 Copyright doc/readme.html | \
        sed -e 's/&ndash;/–/g' \
            -e 's/&copy;/©/g' \
            -e '/<P>/d' > LICENSE
}

build() {
    cd "lua-${pkgver}/src"
    make a CC="psp-gcc" AR="psp-ar rcu" RANLIB="psp-ranlib"
}

package() {
    cd "lua-${pkgver}"
    make INSTALL_TOP="${pkgdir}/psp" INSTALL_BIN="/tmp" TO_BIN="/dev/null" \
        INSTALL_MAN="/tmp" TO_MAN="/dev/null" install

    mkdir -m 755 -p "${pkgdir}/psp/share/licenses/${pkgname}"
    install -m 644 LICENSE "${pkgdir}/psp/share/licenses/${pkgname}"
}
