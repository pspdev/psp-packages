pkgname=lua53
pkgver=5.3.6
pkgrel=3
pkgdesc="a powerful, efficient, lightweight, embeddable scripting language (version 5.3)"
arch=(any)
url="https://lua.org"
license=('MIT')
depends=()
makedepends=()
optdepends=()
conflicts=('lua')
provides=('lua=5.3')
source=("https://www.lua.org/ftp/lua-${pkgver}.tar.gz")
sha256sums=('fc5fd69bb8736323f026672b1b7235da613d7177e72558893a0bdcd320466d60')

prepare() {
    cd "lua-${pkgver}"

    # extract the license from readme
    grep -A21 Copyright doc/readme.html | \
        sed -e 's/&ndash;/–/g' \
            -e 's/&copy;/©/g' \
            -e '/<P>/d' > LICENSE
    sed -i '/#define LUA_32BITS/s/[/*]//g' src/luaconf.h
}

build() {
    cd "lua-${pkgver}/src"
    make a CC="psp-gcc" AR="psp-ar rcu" RANLIB="psp-ranlib"
}

package() {
    cd "lua-${pkgver}"
    make INSTALL_TOP="${pkgdir}/psp" INSTALL_BIN="/tmp" TO_BIN="/dev/null" \
        INSTALL_MAN="/tmp" TO_MAN="/dev/null" install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}
