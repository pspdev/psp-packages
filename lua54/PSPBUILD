pkgname=lua54
pkgver=5.4.6
pkgrel=3
pkgdesc="a powerful, efficient, lightweight, embeddable scripting language (version 5.4)"
arch=(any)
url="https://lua.org"
license=('MIT')
groups=('pspdev-default')
depends=()
makedepends=()
optdepends=()
conflicts=('lua')
provides=('lua=5.4')
source=("https://www.lua.org/ftp/lua-${pkgver}.tar.gz")
sha256sums=('7d5ea1b9cb6aa0b59ca3dde1c6adcb57ef83a1ba8e5432c0ecd06bf439b3ad88')

prepare() {
    cd "lua-${pkgver}"

    # extract the license from readme
    grep -A21 Copyright doc/readme.html | \
        sed -e 's/&ndash;/–/g' \
            -e 's/&copy;/©/g' \
            -e '/<P>/d' > LICENSE
    sed -i '/#define LUA_32BITS/s/0/1/g' src/luaconf.h
}

build() {
    cd "lua-${pkgver}/src"
    make a CC="psp-gcc" AR="psp-ar rcu" RANLIB="psp-ranlib"
}

package() {
    cd "lua-${pkgver}"
    make INSTALL_TOP="${pkgdir}/psp" INSTALL_BIN="/tmp" TO_BIN="/dev/null" \
        INSTALL_MAN="/tmp" TO_MAN="/dev/null" install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}
