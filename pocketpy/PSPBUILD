pkgname=pocketpy
pkgver=1.4.5
pkgrel=2
pkgdesc="Python interpreter for game scripting"
arch=('mips')
license=('MIT')
groups=('pspdev-default')
url="https://pocketpy.dev/"
makedepends=()
source=("https://github.com/pocketpy/pocketpy/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('5f8875db057b9a0b8be324561f1a303c6ace62b5e39e1a082751a76e7edfd9f3')

build() {
    cd "$pkgname-${pkgver}"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_BUILD_TYPE=Release -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake \
        -DPK_BUILD_STATIC_LIB=ON -DPK_BUILD_SHARED_LIB=OFF "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "$pkgname-${pkgver}/build"

    mkdir -m 755 -p "$pkgdir/psp/lib" "$pkgdir/psp/include" "$pkgdir/psp/include/pocketpy" "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 libpocketpy.a "$pkgdir/psp/lib"
    install -m 644 ../include/pocketpy.h "$pkgdir/psp/include"
    install -m 644 ../include/pocketpy/*.h "$pkgdir/psp/include/pocketpy"
    install -m 644 ../LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}
