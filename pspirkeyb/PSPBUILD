pkgname=pspirkeyb
pkgver=r1
pkgrel=5
pkgdesc="a library for using IRDA keyboards with Playstation Portable"
arch=(any)
url="https://github.com/pspdev/pspirkeyb"
license=('LGPL-2.1-only')
groups=('pspdev-default')
depends=()
makedepends=()
optdepends=()
source=("git+https://github.com/pspdev/${pkgname}.git#commit=dd66ec05a946c4896e387bc05c8b130e84f7a21c")
sha256sums=('SKIP')

build() {
    cd "${pkgname}"
    make
}

package() {
    cd "${pkgname}"

    mkdir -m 755 -p "${pkgdir}/psp/sdk/lib" "${pkgdir}/psp/sdk/include"
    cp -v libpspirkeyb/libpspirkeyb.a "${pkgdir}/psp/sdk/lib"
    cp -v libpspirkeyb/include/pspirkeyb.h "${pkgdir}/psp/sdk/include"
    cp -v libpspirkeyb/include/pspirkeyb_rawkeys.h "${pkgdir}/psp/sdk/include"

    mkdir -m 755 -p "${pkgdir}/psp/sdk/samples/irkeyb" "${pkgdir}/psp/sdk/samples/irkeyb/keymap"
    cp libpspirkeyb/keymap/*.ini "${pkgdir}/psp/sdk/samples/irkeyb/keymap"
    cp libpspirkeyb/keymap/README.txt "${pkgdir}/psp/sdk/samples/irkeyb/keymap"
    cp libpspirkeyb/config/pspirkeyb.ini "${pkgdir}/psp/sdk/samples/irkeyb"
    cp asciidemo/main.c "${pkgdir}/psp/sdk/samples/irkeyb"
    cp asciidemo/Makefile.sdk "${pkgdir}/psp/sdk/samples/irkeyb/Makefile"

    mkdir -m 755 -p "${pkgdir}/psp/share/licenses/${pkgname}"
    install -m644 LICENSE "${pkgdir}/psp/share/licenses/${pkgname}"
}

