pkgname=sdl-gfx
pkgver=2.0.26
pkgrel=2
pkgdesc="SDL graphics drawing primitives and other support functions"
arch=('mips')
url="https://www.ferzkopp.net/wordpress/2016/01/02/sdl_gfx-sdl2_gfx/"
license=('ZLIB')
depends=('sdl' 'freetype2')
makedepends=()
optdepends=()
source=("https://github.com/pspdev/SDL_gfx/archive/${pkgver}-psp.tar.gz")
sha256sums=('8a5116995bad20720978bc857588f58b5baf3dbe03f2b2099b339f445abae36d')

prepare() {
    cd "SDL_gfx-$pkgver-psp"
    sed -i 's#@prefix@#${PSPDEV}/psp#' SDL_gfx.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' SDL_gfx.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' SDL_gfx.pc.in
    sed -i 's#@includedir@#${prefix}/include#' SDL_gfx.pc.in
}

build() {
    cd "SDL_gfx-$pkgver-psp"
    ./autogen.sh
    LDFLAGS="-L$(psp-config --pspsdk-path)/lib" LIBS="-lc -lpspuser" \
    ./configure --host psp --prefix=/psp --with-sdl-prefix=$(psp-config --psp-prefix) --disable-mmx
    make
}

package() {
    cd "SDL_gfx-$pkgver-psp"
    make DESTDIR="$pkgdir/" install

    rm "${pkgdir}/psp/lib/libSDL_gfx.la"

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}

