pkgname=sdl-image
pkgver=1.2.13
pkgrel=7
pkgdesc="a simple library to load images of various formats as SDL2 surfaces"
arch=(any)
url="https://www.libsdl.org/projects/old/SDL_image/release-1.2.html"
license=('Zlib')
groups=('pspdev-default')
depends=('sdl' 'libpng' 'jpeg')
makedepends=()
optdepends=()
source=("git+https://github.com/libsdl-org/SDL_image.git#commit=7adb1a70f97d62f1bb307bfb4e6c2fa0bc8c33ab")

sha256sums=('SKIP')

prepare() {
    cd "SDL_image"
    sed -i 's#@prefix@#${PSPDEV}/psp#' SDL_image.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' SDL_image.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' SDL_image.pc.in
    sed -i 's#@includedir@#${prefix}/include#' SDL_image.pc.in
}

build() {
    cd "SDL_image"
    ./autogen.sh
    LDFLAGS="-L$(psp-config --pspsdk-path)/lib" LIBS="-lc -lpspuser" \
    ./configure --host psp --prefix=/psp  --with-sdl-prefix=$(psp-config --psp-prefix)
    make
}

package() {
    cd "SDL_image"
    make DESTDIR="$pkgdir/" install 

    rm "${pkgdir}/psp/lib/libSDL_image.la"

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING "$pkgdir/psp/share/licenses/$pkgname"
}

