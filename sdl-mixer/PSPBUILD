pkgname=sdl-mixer
pkgver=1.2.14
pkgrel=7
pkgdesc="an audio mixer library based on the SDL library"
arch=('mips')
url="https://www.libsdl.org/projects/old/SDL_mixer/release-1.2.html"
license=('Zlib')
groups=('pspdev-default')
depends=('sdl' 'libmikmod' 'libogg' 'libvorbis')
makedepends=()
optdepends=()
source=("git+https://github.com/libsdl-org/SDL_mixer.git#commit=78cde6dd63222f9345dda38e8b7dca836aa55803")

sha256sums=('SKIP')

prepare() {
    cd "SDL_mixer"
    wget -O build-scripts/config.sub "http://git.savannah.gnu.org/gitweb/?p=autoconf.git;a=blob_plain;f=build-aux/config.sub"
    sed -i 's#@prefix@#${PSPDEV}/psp#' SDL_mixer.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' SDL_mixer.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' SDL_mixer.pc.in
    sed -i 's#@includedir@#${prefix}/include#' SDL_mixer.pc.in
}

build() {
    cd "SDL_mixer"
    ./autogen.sh
    LDFLAGS="-L$(psp-config --pspsdk-path)/lib" LIBS="-lc -lpspuser" \
    ./configure --host psp --with-sdl-prefix=$(psp-config --psp-prefix) \
    --disable-music-mp3 --prefix=/psp \
    --enable-music-mod --disable-music-cmd
    make
}

package() {
    cd "SDL_mixer"
    make DESTDIR="$pkgdir/" install

    rm "${pkgdir}/psp/lib/libSDL_mixer.la"

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING "$pkgdir/psp/share/licenses/$pkgname/COPYING"
}

