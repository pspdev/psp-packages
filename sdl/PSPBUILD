pkgname=sdl
pkgver=1.2.15
pkgrel=4
pkgdesc="a library designed to provide low level access to audio, input, and graphics hardware"
arch=('mips')
url="https://www.libsdl.org/download-1.2.php"
license=('LGPL-2.1')
depends=('pspirkeyb')
makedepends=()
optdepends=()
source=("git+https://github.com/pspdev/SDL.git#commit=42ee00502244fc60e76ae395fcee29d82f64c369")
sha256sums=('SKIP')

prepare() {
    cd "SDL"
    sed -i 's#@prefix@#${PSPDEV}/psp#' sdl-config.in sdl.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' sdl-config.in sdl.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' sdl-config.in sdl.pc.in
    sed -i 's#@includedir@#${prefix}/include#' sdl-config.in sdl.pc.in
}

build() {
    cd "SDL"
    ./autogen.sh
    LDFLAGS="$LDFLAGS -L$(psp-config --pspsdk-path)/lib -L$(psp-config --psp-prefix)/lib -lc -lpspuser" \
    LIBS="$LIBS -lc -lpspuser" \
    CFLAGS="-Wno-error=incompatible-pointer-types" \
    ./configure --host=psp --prefix=/psp --enable-pspirkeyb
    make
}

package() {
    cd "SDL"
    make DESTDIR="$pkgdir/" install

    rm "${pkgdir}/psp/lib/libSDL.la"
    rm "${pkgdir}/psp/lib/libSDLmain.la"

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING "$pkgdir/psp/share/licenses/$pkgname"
}

