pkgname=sdl2-mixer
pkgver=2.8.0
pkgrel=2
pkgdesc="an audio mixer library based on the SDL2 library"
arch=('mips')
url="https://wiki.libsdl.org/SDL2_mixer/FrontPage"
license=('Zlib')
groups=('pspdev-default')
depends=('sdl2' 'libmodplug' 'libvorbis' 'libogg')
makedepends=()
optdepends=()
source=(
    "https://github.com/libsdl-org/SDL_mixer/releases/download/release-${pkgver}/SDL2_mixer-${pkgver}.tar.gz"
    "CMakeLists.txt.sample"
    "main.c.sample"
    "https://pspdev.github.io/resources/test.ogg"
)
sha256sums=(
    "1cfb34c87b26dbdbc7afd68c4f545c0116ab5f90bbfecc5aebe2a9cb4bb31549"
    "SKIP"
    "SKIP"
    "b728bb95b6018ba0bb5675d614f2e25d056d04c6b471f4f080623c570f905e01"
)

prepare() {
    cd "${srcdir}/SDL2_mixer-${pkgver}"
    sed -i '/^prefix=/s/=.*$/=${PSPDEV}\/psp/' SDL2_mixer.pc.in
}

build() {
    cd "${srcdir}/SDL2_mixer-${pkgver}"
    mkdir -p build && cd build
    cmake -DCMAKE_TOOLCHAIN_FILE="${PSPDEV}/psp/share/pspdev.cmake" -DCMAKE_INSTALL_PREFIX=/psp \
        -DBUILD_SHARED_LIBS=OFF -DCMAKE_POSITION_INDEPENDENT_CODE=OFF -DSDL2MIXER_DEPS_SHARED=OFF \
        -DSDL2MIXER_INSTALL=ON -DSDL2MIXER_SAMPLES=OFF -DSDL2MIXER_FLAC=OFF -DSDL2MIXER_OPUS=OFF \
        -DSDL2MIXER_MIDI=OFF -DSDL2MIXER_VORBIS=VORBISFILE -DSDL2MIXER_VORBIS_VORBISFILE_SHARED=OFF \
        -DSDL2MIXER_MOD=ON -DSDL2MIXER_MOD_MODPLUG=ON -DSDL2MIXER_MOD_MODPLUG_SHARED=OFF \
        -DSDL2MIXER_MOD_XMP=OFF -DSDL2MIXER_MP3=ON -DSDL2MIXER_MP3_MINIMP3=ON -DSDL2MIXER_MP3_MPG123=OFF \
        -DSDL2MIXER_WAVPACK=OFF -DCMAKE_BUILD_TYPE=Release .. "${XTRA_OPTS[@]}" || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    mkdir -p "${pkgdir}/psp/sdk/samples/sdl2/mixer/"
    install -m 644 "${srcdir}/main.c.sample" "${pkgdir}/psp/sdk/samples/sdl2/mixer/main.c"
    install -m 644 "${srcdir}/CMakeLists.txt.sample" "${pkgdir}/psp/sdk/samples/sdl2/mixer/CMakeLists.txt"
    install -m 644 "${srcdir}/test.ogg" "${pkgdir}/psp/sdk/samples/sdl2/mixer/test.ogg"

    cd "${srcdir}/SDL2_mixer-${pkgver}/build"
    make --quiet DESTDIR="${pkgdir}" ${MAKEFLAGS} install

    mv "${pkgdir}/psp/share/licenses/SDL2_mixer" "${pkgdir}/psp/share/licenses/${pkgname}"
}
