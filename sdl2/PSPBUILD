pkgname=sdl2
pkgver=2.32.8
pkgrel=3
pkgdesc="A library designed to provide low level access to audio, input, and graphics hardware"
arch=(any)
url="https://wiki.libsdl.org/SDL2/FrontPage"
license=('Zlib')
groups=('pspdev-default')
depends=('libpspvram' 'pspgl')
makedepends=()
optdepends=()
provides=('sdl2-main')
source=(
    "https://github.com/libsdl-org/SDL/releases/download/release-${pkgver}/SDL2-${pkgver}.tar.gz"
    "CMakeLists.txt.sample"
    "main.c.sample"
)
sha256sums=(
    "0ca83e9c9b31e18288c7ec811108e58bac1f1bb5ec6577ad386830eac51c787e"
    "SKIP"
    "SKIP"
)

prepare() {
    cd "${srcdir}/SDL2-${pkgver}"
    sed -i '/^prefix=/s/=.*$/=${PSPDEV}\/psp/' sdl2.pc.in
}

build() {
    cd "${srcdir}/SDL2-${pkgver}"
    mkdir -p build && cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE="${PSPDEV}/psp/share/pspdev.cmake" -DCMAKE_INSTALL_PREFIX=/psp \
        -DBUILD_SHARED_LIBS=OFF -DSDL_TESTS=OFF -DSDL_TEST=OFF "${XTRA_OPTS[@]}" -DCMAKE_BUILD_TYPE=Release \
        .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    mkdir -p "${pkgdir}/psp/sdk/samples/sdl2/basic/"
    install -m 644 "${srcdir}/main.c.sample" "${pkgdir}/psp/sdk/samples/sdl2/basic/main.c"
    install -m 644 "${srcdir}/CMakeLists.txt.sample" "${pkgdir}/psp/sdk/samples/sdl2/basic/CMakeLists.txt"

    cd "${srcdir}/SDL2-${pkgver}/build"
    make --quiet DESTDIR="${pkgdir}" $MAKEFLAGS install

    mv "${pkgdir}/psp/share/licenses/SDL2" "${pkgdir}/psp/share/licenses/${pkgname}"
}
