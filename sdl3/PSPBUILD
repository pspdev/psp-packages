pkgname=sdl3
pkgver=3.4.0
pkgrel=1
pkgdesc="A library designed to provide low level access to audio, input, and graphics hardware"
arch=(any)
url="https://wiki.libsdl.org/SDL3/FrontPage"
license=('Zlib')
groups=('pspdev-default')
depends=('libpspvram' 'pspgl')
makedepends=()
optdepends=()
provides=()
source=(
    "https://github.com/libsdl-org/SDL/releases/download/release-${pkgver}/SDL3-${pkgver}.tar.gz"
    "14913.patch"
)
sha256sums=(
    "082cbf5f429e0d80820f68dc2b507a94d4cc1b4e70817b119bbb8ec6a69584b8"
    "559ab3dec2580b132cd843c67551b1ed1b5bf133d572a03dd54da9694b933f4a"
)

prepare() {
    cd "${srcdir}/SDL3-${pkgver}"
    sed -i 's#@SDL_PKGCONFIG_PREFIX@#${PSPDEV}/psp#' cmake/sdl3.pc.in
    sed -i 's#@LIBDIR_FOR_PKG_CONFIG@#${prefix}/lib#' cmake/sdl3.pc.in
    sed -i 's#@INCLUDEDIR_FOR_PKG_CONFIG@#${prefix}\/include#' cmake/sdl3.pc.in

    patch -p1 < ../14913.patch || { exit 1; }
}

build() {
    cd "${srcdir}/SDL3-${pkgver}"
    mkdir -p build && cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE="${PSPDEV}/psp/share/pspdev.cmake" -DCMAKE_INSTALL_PREFIX=/psp \
        -DBUILD_SHARED_LIBS=OFF -DSDL_TEST_LIBRARY=OFF "${XTRA_OPTS[@]}" -DCMAKE_BUILD_TYPE=Release \
        .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "${srcdir}/SDL3-${pkgver}/build"
    make --quiet DESTDIR="${pkgdir}" $MAKEFLAGS install

    mv "${pkgdir}/psp/share/licenses/SDL3" "${pkgdir}/psp/share/licenses/${pkgname}"
}
