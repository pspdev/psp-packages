pkgname=zziplib
pkgver=0.13.76
pkgrel=2
pkgdesc="provides read access to zipped files in a zip-archive"
arch=('mips')
url="http://zziplib.sourceforge.net/"
license=('ZLIB')
groups=('pspdev-default')
depends=('zlib')
makedepends=()
optdepends=()
source=("https://github.com/gdraheim/zziplib/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('08b0e300126329c928a41b6d68e397379fad02469e34a0855d361929968ea4c0')

build() {
    cd "$pkgname-$pkgver"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        -DZZIPSDL=OFF -DZZIPBINS=OFF -DZZIPTEST=OFF -DZZIPDOCS=OFF -DGCOV_PATH=psp-gcov "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "$pkgname-$pkgver/build"
    make --quiet $MAKEFLAGS install
    cd ..

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING.LIB "$pkgdir/psp/share/licenses/$pkgname"
}

